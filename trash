#!/usr/bin/perl -w

use strict;

our $gvfs_trash= "/usr/bin/gvfs-trash";

if (-x $gvfs_trash) {
    exec $gvfs_trash, @ARGV;
}

my $name= 'trash';
my $trash;
if ($ENV{TRASHCAN}) {
    $trash= $ENV{TRASHCAN}
} elsif (-e $ENV{HOME}."/Desktop/Trash/") {
    $trash=$ENV{HOME}."/Desktop/Trash/";
} elsif (-e $ENV{HOME}."/Desktop/Mülleimer/") {
    $trash=$ENV{HOME}."/Desktop/Mülleimer/";
} elsif (-e $ENV{HOME}."/.Trash/") {
    $trash=$ENV{HOME}."/.Trash/";
} else {
    die "Konnte Deinen Trashcan nicht finden. ";
}

{
    my $localtime;
    sub _localtime {
	$localtime||=do {
	    "".localtime()
	}
    }
}
{
    my $n;
    sub _appendi {
	my $oldn= $n++;
	_localtime . (defined ($oldn) ? "\#$oldn" : "");
    }
}

if (@ARGV) {
    if ($ARGV[0]=~ /^--?h(elp)?$/) {
	die "$name files ..
      Move each file to ~/Desktop/Trash, taking care not to overwrite
      existing files in trash (appending localtime() string if needed).
    "} else {
	if ($ARGV[0]=~ /^--$/) { shift @ARGV }
	for my $file (@ARGV) {
	    my $onlyname=$file;
	    $onlyname=~s|/$||; # remove ending slash from directories
	    $onlyname=~s|^.*/||;
	    if ($onlyname eq '.') {
		warn "Ignoring '.'\n";
		next;
	    }
	    if ($onlyname eq '..') {
		warn "Ignoring '..'\n";
		next;
	    }
	    if (-e $trash.$onlyname) {
		mv( $file, $trash.$onlyname." (trashed "._appendi.")");
	    } else {
		mv($file, $trash);
	    }
	}
    }
}

sub mv { #rename would give 'Invalid cross-device link'
    if (fork) {
	wait;
    } else {
	exec '/bin/mv', '--', @_
    }
}
