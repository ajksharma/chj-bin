#!/usr/bin/perl -w

# Mon Nov  4 17:16:26 GMT 2013
(my $email='ch%christianjaeger,ch')=~ tr/%,/@./;

use strict;

# A "df" that shows the correct mountpoints when run without an
# argument even if run chrooted.

# I still wonder why /proc/mounts, which is meant to be used as
# symlink target for /etc/mtab, doesn't work usefully for chroots,
# while /proc/self/mountinfo does have the correct information but not
# the right format.


use Chj::xopen 'xopen_read';


# XX move to library?

{
    package CHJ::Mount;
    sub dev { my $s=shift; $$s[0] }
    sub mountpoint { my $s=shift; $$s[1] }
    sub type { my $s=shift; $$s[2] }
    sub options { my $s=shift; $$s[3] }
    sub fsck { my $s=shift; $$s[4] }
    sub fsck_order { my $s=shift; $$s[5] }
}

sub mounts {
    my $f= xopen_read "/proc/mounts";
    my @m;
    while (<$f>) {
	my @f= split " ";
	@f == 6 or die "line in /proc/mounts with different number of fields";
	push @m, bless \@f, "CHJ::Mount";
    }
    $f->xclose;
    @m
}

{
    package CHJ::Mountinfo;
    our @fields=qw(dunno1 dunno2 inode from mountpoint options
		   dunno3 type1 type2 moreoptions);
    my $i=0;
    for (@fields) {
	no strict 'refs';
	my $idx= $i;
	*$_= sub { my $s=shift; $$s[$idx] };
	$i++
    }
}

sub mountinfos {
    my $path= "/proc/self/mountinfo";
    my $f= xopen_read $path;
    my @m;
    while (<$f>) {
	my @f= split " ";
	@f == @CHJ::Mountinfo::fields
	  or die "line in $path with different number of fields: '$_'";
	push @m, bless \@f, "CHJ::Mountinfo";
    }
    $f->xclose;
    @m
}

# /library


our $suppress_type=
  { sysfs=> 1,
    proc=> 1,
    devpts=> 1,
    usbfs=> 1,
    fusectl=> 1,
    tmpfs=> 0 };

sub mountpoints ($) {
    my ($all)=@_;
    map {
	#do{use Chj::repl; repl;};
	if ($all) {
	    $_->mountpoint
	} else {
	    my $t= $_->type2;
	    if ($$suppress_type{$t}) {
		()
	    } else {
		$_->mountpoint
	    }
	}
    } mountinfos
}

sub my_df {
    exec "df", "--", @_;
}

sub df {
    exec "df", @_
}

if (@ARGV) {
    if (@ARGV==1 and ($ARGV[0] eq "-a" or $ARGV[0] eq "--all")) {
	my_df mountpoints(1)
    } else {
	df @ARGV
    }
} else {
    my_df mountpoints(0)
}

#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;
