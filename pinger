#!/usr/bin/perl -w

use strict;

$0=~ /([^\/]+)$/s or die "?";
my $myname=$1;
sub usage {
    print "$myname address
  Continuously pings address and beeps or pops up a dialog if it can't reached.
  For use with bad network cabling so you know when you've lost connectivity.
";
exit;
}


use Getopt::Long ':config'=> 'bundling';
use Fcntl;
use Time::HiRes 'usleep';
use Chj::xperlfunc;

our $verbose;
our $opt_popup;
usage unless GetOptions (
			 'verbose|v'=>\$verbose,
			 'dialog|popup|p'=>\$opt_popup,
			);

usage unless @ARGV==1;
my $address= shift;
usage if $address=~ /^-/;



our ($trouble_on_cb, $trouble_off_cb,
     $expecttime);

if ($opt_popup) {
    $expecttime= 2; # since the popup is stealing time in the waitpid right?
    my $childpid;
    my $waitpid= sub {
	waitpid $childpid,0;
	$childpid= undef;
    };	
    my $maybe_killchild= sub {
	if ($childpid) {
	    kill 9, $childpid;
	    &$waitpid;
	}
    };
    $trouble_on_cb= sub {
	&$maybe_killchild;
	if ($childpid= xfork) {
	    # nothing
	} else {
	    xexec "xmessage", localtime()." $myname: network down event"
	}
    };
    $trouble_off_cb= sub {
	&$maybe_killchild;
    };
} else {
    $expecttime= 2;
    $trouble_on_cb= sub {
	xxsystem "/root/bin/beeponserver";
	usleep 150000;
	xxsystem "/root/bin/beeponserver";
    };
    $trouble_off_cb= sub {
	xxsystem "/root/bin/beeponserver";
    };
}



pipe READ,WRITE
  or die $!;

my $pid=xfork;

if ($pid) {
    #father
    close WRITE;
    $SIG{ALRM}= sub {
        die "ALRM\n";
    };
    my $lastbeep=0;
    while(1) {
        eval {
            alarm $expecttime;
            while (<READ>) {
		# success, got ping back
                if ($lastbeep) {
		    # been in trouble, not any more
		    alarm 0;
                    $lastbeep=0;
                    print localtime()."\tWEITER\n";
                    &$trouble_off_cb;
                }
                alarm $expecttime;
                print "$_" if $verbose;
            }
        };
        die unless $@ eq "ALRM\n";
        my $time=time;
        if ($time-$lastbeep > 10) {
            $lastbeep=$time;
            print localtime($time)."\tUNTERBRUCH\n";
	    &$trouble_on_cb;
        }
    }
}
else {
    close READ;
    open STDOUT,">&WRITE";
    exec "ping",$address;
    die
}
