#!/usr/bin/perl -w

# Mit Feb 13 13:35:53 CET 2008
(my $email='pflanze%gmx,ch')=~ tr/%,/@./;

use strict;

our $timeout=10;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname socketpath

  Options:
  --timeout seconds   instead of the default $timeout

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   "timeout"=> \$timeout,
	   ) or exit 1;
usage unless @ARGV==1;

our $socketpath= shift @ARGV;

use IO::Socket::UNIX;

$SIG{ALRM}= sub {
    die "$myname on '$socketpath': timed out\n";
};
alarm $timeout;

our $socket= IO::Socket::UNIX->new
  (
   Type=> SOCK_STREAM,
   Peer=> $socketpath,
  )
  or die "opening socket '$socketpath': $!";#!

$socket->send("trigger\n")
  or die "send: $!";
my $result= <$socket>;
if ($result ne "ok\n") {
    die "$myname: did not get an ok from the server, but: $result";
}
$socket->close
  or die "close: $!";

