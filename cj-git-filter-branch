#!/usr/bin/perl -w

# Mon Jun  2 15:02:18 CEST 2008
(my $email='XXX%YYY,ch')=~ tr/%,/@./;

use strict;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);

use Chj::xperlfunc;
use Chj::singlequote "singlequote_sh";

sub _xxsystem {
    use Data::Dumper "Dumper";
    print "xxsystem ", Dumper \@_;
    xxsystem @_;
}

our $removefiles= sub {
    # @ARGV being the list of files to remove
    _xxsystem(
	     'git',
	     'filter-branch',
	     '--index-filter',
	     'git update-index '
	     .join(' ',
		   map {
		       "--remove ".singlequote_sh($_)
		   } @ARGV)
	     .' HEAD',
	     '--commit-filter',
	     'if [ z"$1" = z`git rev-parse $3^{tree}` ]
      then skip_commit "$@"
      else git commit-tree "$@"; fi',
	    );
};

sub CmdSetter ( $ );#heh
our $cmds=
  [
   ["remove-files" => CmdSetter($removefiles),
    "remove all given file paths from repo (including their commit if empty)"],
  ];

sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    my $cmdsstr= join("",
		      map {
			  my ($nam,$ptr,$desc)=@$_;
			  "   --$nam  $desc\n"
		      }
		      @$cmds);
    print "$myname --command [ commandarguments ]

$cmdsstr
  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

our $command;
sub CmdSetter ( $ ) {
    my ($cmd)=@_;
    sub {
	if ($command) {
	    usage "can't give more than one command, currently";
	}
	$command= $cmd
    }
}

use Getopt::Long;
our $verbose=0;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   map {
	       @$_[0,1]
	   } @$cmds
	  ) or exit 1;
usage unless @ARGV;

&$command;


#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;
