#!/usr/bin/perl -w

# Don Aug 19 00:23:44 MEST 2004
(my $email='pflanze%gmx,ch')=~ tr/%,/@./;

use strict;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname directory [directory2 ..]
  Outputs name of the last modified *file* which does not begin
  with a dot or ~ or end in ~ in given directory

  --fullpath  full path (including directory) is output, not only
              the filename.

  (Christian Jaeger <$email>)
";
exit @_ ? 1 : 0;
}

my @args;
my $DEBUG=0;
my $opt_fullpath;
for (my $i=0; $i<=$#ARGV; $i++) {
    local $_=$ARGV[$i];
    if (/^--?h(elp)?$/) {
    	usage
    } elsif ($_ eq '--') {
    	push @args, @ARGV[$i+1..$#ARGV];
    	last;
    } elsif (/^--?d(ebug)?$/) {
	$DEBUG=1;
    } elsif (/^--?full(?:-?path(?:=(.*))?)?$/) {
        if (defined $1) {
            $opt_fullpath=$1
        } else {
            $opt_fullpath=1
        }
    } elsif (/^-./) {
    	usage("Unknown option '$_'\n");
    } else {
    	push @args, $_
    }
}
usage unless @args;

$opt_fullpath = 1 if @args > 1;

sub showit {
    my ($dir)=@_;
    #use Chj::singlequote; warn "dir=".singlequote($dir);
    opendir DIR, $dir
      or die "opendir '$dir': $!";
    $dir=~ s|/+\z||s;
    # ^ for path appending. so that '/' still works for opendir.

    my @items;
    while (defined ($_=readdir DIR)){
	next if /^[.~]/ or /~\z/;
        my @s=lstat "$dir/$_" or die "lstat '$dir/$_': $!";
        if (-f _) {
	    push @items,[$opt_fullpath ? "$dir/$_" : $_,
			 $s[9]];
	}
    }

    if (@items) {
	print (
	       (sort { $b->[1] <=> $a->[1] } @items)[0]->[0]
	      );# heh, -> after the [0] is necessary here
	print "\n";
    } # else  return error status ?
}

for (@args) {
    showit $_
}

