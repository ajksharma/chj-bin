#!/usr/bin/perl -w

# Fri Mar 27 09:51:23 EDT 2009
(my $email='XXX%YYY,ch')=~ tr/%,/@./;

use strict;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);

sub ignorepath {
    @_==2 or die;
    my ($gitbase,$branchname)=@_;
    "$gitbase/$myname.ignore/$branchname"
};

sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    my $where= ignorepath (".git",'$branchname');
    print "$myname branchname

  Cherry-pick all patches that branch has which the current branch
  doesn't have (hm also older ones? or just from the latest fork
  point??) and apply them unless their sha1 is listed in
  $where .

  Options:
  --dry-run

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
our ($opt_dry);
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   "dry-run"=> \$opt_dry,
	   ) or exit 1;
usage unless @ARGV==1;

our ($branch)=@ARGV;#NOT @_ WHEN WILL I REMEMBER THAT.

# hm habe git modul sachen 'hm'.

#use Chj::Git::Functions ':all';
#our $git_dir= xgit_dir;

# well. what does this help, vs. using Git.pm

use Git;
{
    package CJ_GIT_PICK_PICKY::Git::Commit;
    sub is_merge {
	my $s=shift;
	@{$$s[1]}>1
    }
    sub sha1 {
	my $s=shift;
	$$s[0]
    }
}
{
    package CJ_GIT_PICK_PICKY::Git;
    use base 'Git';
    # ^ we assume here that it's a fixed Git.pm
    use Chj::Random::Formatted ();# 'random_passwd_string';

    sub log_sha1_and_parents {
	my $s=shift;
	@_==1 or die "need revision (range) argument";
	my ($fh,$ctx)=$s->command_output_pipe ("log",'--pretty=format:%H:%P',@_);
	my @log=
	  #fullyeager..
	  map {
	      chomp;
	      my ($commit,$parents)= split /:/,$_,2
		or die "invalid line '$_'";
	      my @parents= split / /,$parents;
	      bless [$commit, \@parents],"CJ_GIT_PICK_PICKY::Git::Commit"
	  } <$fh>;
	$s->command_close_pipe ($fh,$ctx);
	\@log
    }
    my $pre='(cherry picked from commit ';
    my $sha1_re= qr{[0-9a-f]{40}};
    my $post= ')';
    my $re= qr/\Q$pre\E($sha1_re)\Q$post\E\s*\z/;
    sub known_cherry_picks {
	my $s=shift;
	@_==0 or die "expecting 0 arguments";
	my $marker= Chj::Random::Formatted::random_passwd_string (8);
	my ($fh,$ctx)=$s->command_output_pipe ("log",'--pretty=format:%H:%s %b'.$marker,@_);
	local $/=$marker;
	my $known={}; # origcommit=> [newcommits]
	while (<$fh>) {
	    chomp;
	    # somehow we get whitespace (a newline actually) before the commit id starting from the 2nd record. (why? so does git imply a newline after all ?  maybe I forgot to give some  whatever -z or so option. or so.toolazytolooknow.havemyoldscmcode.). strip it.
	    s/^\s+//s;
	    my ($commit,$msg)= split /:/,$_,2
	      or die "invalid input record '$_'";
	    $commit=~ m/^$sha1_re\z/
	      or die "BUG: not a sha1: '$commit'";
	    ##^ turn that into an assert statement. etcetc.
	    if ($msg=~ m/$re/s) {
		my $origcommit=$1;
		push @{$$known{$origcommit}},$commit
	    }
	}
	$s->command_close_pipe ($fh,$ctx);
	$known
    }
    sub maybe_current_branch {
	my $s=shift;
	#hm I did this already somewhere. branches method. wherever.
	my ($fh,$ctx)=$s->command_output_pipe ("branch");
	my $current_branch;
	while (<$fh>) {
	    if (s/^\*\s+//s) {
		chomp;
		## some further checks needed?
		$current_branch= $_;
		# stop? well. avoid sigpipe?  btw yeh i also did solve *that* already.
		#hey, \MYLANG{expect and check for void in non-terminal begin form positions.
	    }
	}
	$s->command_close_pipe ($fh,$ctx);#(hope this dies upon errors,right?. ...)
	$current_branch
    }
}

our $git= CJ_GIT_PICK_PICKY::Git->repository(Directory=> ".");#k? yep works even from subdirs.


#COPY from above:
my $sha1_re= qr{[0-9a-f]{40}};
#/COPY

use Chj::xopen 'xopen_read';

our $ignore= do {
    my $current_branch= $git->maybe_current_branch
      or die "not on a branch?";
    our $ignorepath= ignorepath ($git->repo_path, $current_branch); # *not* $branch!
    my $f= xopen_read $ignorepath;
    my $ignore={};
    my $z=0;
    while (<$f>) {
	$z++;
	s/#.*//;
	s/^\s*//s;
	s/\s*\z//s;
	next unless length;
	#m/^$sha1_re\z/
	#  or die "file '$ignorepath' line $z does not match sha1 sum pattern: '$_'";
	m/^($sha1_re)\b/
	  or die "file '$ignorepath' line $z does not start with a sha1: '$_'";
	$$ignore{$1}++
    }
    $f->xclose;
    $ignore
};

our $known_cherry_picks= $git->known_cherry_picks;
#use Chj::Backtrace; use Chj::repl; repl;

# NOTE: git cherry-pick has a --mainline option, so I could maybe even
# pick merges. But maybe I *still* don't want to? (dunnoreally.)

for my $commit (reverse grep { not $_->is_merge } @{$git-> log_sha1_and_parents ("HEAD..$branch")}) {
    #( grep reverse   warum macht cperl mode verschiedene farben (f diese 2 kommandos) h.?.) (hautpsachefrbg)
    #warn "looking at commit '$commit'..\n";#
    my $commitsha1= $commit->sha1; #grrrr.
    if ($$ignore{$commitsha1}) {
	print "to be ignored: $commitsha1\n";
    } else {
	if (my $known= $$known_cherry_picks{$commitsha1}) {
	    print "already picked: ".$commitsha1."\n"; #" (at @$known)\n"; is too long.
	} else {
	    if ($opt_dry) {
		print "would cherry-pick ".$commitsha1."\n";
	    } else {
		print "cherry-picking $commitsha1..\n";
		$git->command ("cherry-pick", "-x", $commitsha1);
	    }
	}
    }
}

#use Chj::ruse;
