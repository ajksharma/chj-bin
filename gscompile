#!/usr/bin/perl -w

# Fre Feb 25 20:29:10 CET 2005
(my $email='XXX%YYY,ch')=~ tr/%,/@./;

use strict;
use Chj::xperlfunc;

sub sys_is_x86 {
    my $sys= `uname -m`;
    $sys =~ /86/ and not $sys=~/86_64/
}

my @cflags=
  (
   "-O3",
   "-fomit-frame-pointer",
   #"-fstrict-aliasing",
   #"-mcpu=i586",
   #"-mcpu=athlon",
   #"-mcpu=i686",
   (sys_is_x86() ? "-mpreferred-stack-boundary=2" : ()),
   #"-Wall",#not interesting.
  );

$0=~ /(.*?)([^\/]+)\z/s or die "?"; 
my ($mydir, $myname)=($1,$2); 
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname sourcefile(s)

  automatic compiler, kind of like csc for chicken. Does *not*
  currently check for already available still-valid results

  (Christian Jaeger <$email>)
";
exit @_ ? 1 : 0;
}

my @args;
my $VERBOSE=0;
my $opt_debug;
my $opt_track_scheme;
my $opt_warnings=1;
my $opt_chpax;
for (my $i=0; $i<=$#ARGV; $i++) {
    local $_=$ARGV[$i];
    if (/^--?h(elp)?$/) {
    	usage
    } elsif ($_ eq '--') {
    	push @args, @ARGV[$i+1..$#ARGV];
    	last;
    } elsif (/^--?d(ebug)?$/) {
	$opt_debug=1;
	$opt_track_scheme=1 unless defined $opt_track_scheme;#oder so. muss explizit abschalten damit das nöd kommt.
	$opt_chpax=1 unless defined $opt_chpax;
    } elsif (/^--?(no-?)t(rack(-scheme)?)?$/) {
	$opt_track_scheme= ! $1;
    } elsif (/^--?(no-?)w(arn(ings)?)?$/) {
	$opt_warnings= ! $1;
    } elsif (/^--?(v+)(erbose)?$/) {
	$VERBOSE+=length($1);
#     } elsif (/^--?X(?:XXX(?:=(.*))?)?$/) {
#         if (defined $1) {
#             $XXX=$1
#         } else {
#             $XXX=$ARGV[++$i] or usage "missing argument for '$_' option";
#         }
    } elsif (/^-./) {
    	usage("Unknown option '$_'\n");
    } else {
    	push @args, $_
    }
}
usage unless @args;

for (@args) {
    /\.(scm|c)$/
      or not /\.\w{1,4}$/
	or die "invalid argument '$_': only accepting .c and .scm files currently (files without suffix are meant to be .scm files)";
}


sub Do {
    print join(" ",@_),"\n";
    xxsystem @_
}


my @schemesource=
  map{ local $_=$_; s/\.scm$//; $_} grep { ! /\.c$/ } @args;


Do "gsc",
  "-:f8",
  "-link",
  ($opt_debug ? "-debug" : ()),
  ($opt_track_scheme ? "-track-scheme" : ()),
  ($opt_warnings ? "-warnings" : ()),
  @schemesource;


my @allcsource=
  map{ local $_=$_; s/\.(scm|c)$//; "$_.c"} @args;

my $linkfile= do {
    # guess: last scm file?
    $schemesource[-1]."_.c"
};
my $executablename= do {
    $schemesource[-1]
};

# assuming all Gambit dirs are in the same location:
my $gambinstdir= `gsc -e '(display (path-expand "~~"))'`;

Do "gcc",
  @cflags,
  ($opt_debug ? "-g" : ()),
  @allcsource,
  $linkfile,
  "-I$gambinstdir/include",
  "-L$gambinstdir/lib",
  "-Wl,-rpath", "$gambinstdir/lib",
  # ^ so that the path needn't be in the ld.so.conf
  "-lgambc",
  # according to docs only for linux, not macosx:
  "-lm",
  "-lutil",
  "-ldl",
  "-o",$executablename,
  ;

if ($opt_chpax) {
    Do "chpax","-sr",$executablename
}
