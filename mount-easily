#!/usr/bin/perl -w

# Mon Apr 27 15:33:14 EDT 2009
(my $email='XXX%YYY,ch')=~ tr/%,/@./;

use strict;

# how much older the next-older entry can be to still be automatically mounted
our $auto_max_delay= 1; #seconds

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname --auto|device(s)

  Automatically turns the device path into a mount path and mounts the
  device on that path. Errors out when something is mounted on the
  choosen path already.

  Automatically uses -o loop if the 'device' path is a regular file.

  Options:
  -c|--create-dir  create mount path [if it doesn't exist already]
  --private create mount path as \$dest_path/mnt/ subdirectory and
            create \$dest_path with 0700 permissions. If mount path
            already exists, require it to have a mnt/ subdirectory
            (and use that, which will be done anyway, but with
            --private it fails if that directory doesn't exist) [and
            check the parent dir for 0700 perms? well no for now]
  --ro|--readonly
  [--noatime wll use as default anyway ok?]
  --auto    mount the latest entries in /dev/disk/by-uuid/
            (going backward from the latest and stopping when an entry
            is more than $auto_max_delay second(s) younger)

  Notes:
  - if available, automatically uses the uuid of partitions as luks dev
    mapper names and mount points
  - tip: after having mounted easily a partition:
     umount /mnt/\$uuid
     cd /mnt
     mv \$uuid \$what_you_want_to_see_in_mount_tab
     ln -s \$what_you_want_to_see_in_mount_tab \$uuid

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
#our @mountoptions=(); ehr no,
our $mountoptions= { noatime => 1 };
our $opt_createdir;
our $opt_private;
our $opt_auto;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   "ro|readonly"=> sub {
	       $$mountoptions{ro}=1;
	   },
	   "noatime"=> sub {
	       $$mountoptions{noatime}=1;
	   },
	   "c|create-dir"=> \$opt_createdir,
	   "private"=> \$opt_private,
	   "auto"=> \$opt_auto,
	  ) or exit 1;

use Chj::Unix::Mount 'mounted','mount';
use Chj::xperlfunc qw(xmkdir basename dirname xstat xxsystem);
sub mydie { #forever
    die "$myname: @_\n";#hm or join  ?.  a forever-*question*
}

use Chj::xrealpath;
use Chj::xopendir;
#sub dev_to_maybe_
sub maybe_uuid_from_dev {
    my ($devpath)=@_;
    my $search= xrealpath $devpath;
    my $base= "/dev/disk/by-uuid";
    my $d= xopendir $base;
    # local chdir wl. wt gmb provides. eben manuell dieses machen hier.
    while (defined (my$item=$d->xnread)) {
	my $found= xrealpath "$base/$item";
	if ($found eq $search) {
	    return $item
	}
    }
    $d->xclose;
    undef
}

use Chj::IO::Command;

sub try_file {
    # do not throw exception on error, but return an error (not false in perl5..)
    # well why not the tuple case. boolean   there  still.
    # hm.   (vs multiple cont vs x..)
    my ($devpath)=@_;
    # could also be file. no problem.
    my $devrealpath= xrealpath $devpath; # to resolve symlinks
    my $in= Chj::IO::Command->new_sender
      ("file","--brief","--special-files",$devrealpath);
    my $cnt= $in->xcontent;
    my $res= $in->xfinish;
    ($res==0,$cnt)
}

sub maybe_luks { # not just is_luks. ok?. I mean: give luks details if luks.
    my ($devpath)=@_;
    my ($ok,$cnt)= try_file $devpath;
    if ($ok) {
	if ($cnt=~ /^LUKS encrypted/) {
	    $cnt
	} else {
	    undef
	}
    } else {
	die "error checking '$devpath': $cnt";
    }
}

use Chj::xperlfunc;

sub mount_easily {
    my ($device_path, $verbose, $ignore_if_mountpoint_missing)=@_;
    my $device_fileinfo;
    if ($verbose) {
	print "mounting '$device_path'..\n";
    }
    #my $dir= dirname $device_path;###  how to use it ?. notatalll ?

    # check whether we need -o loop: (safe enough?hopeso)
    {
	my $s= xstat $device_path;
	##BUG,right: the following results in effects on all
	##'subsequently' processed mounts."!". (Even/especially in --auto mode..)
	if ($s->is_file) {#hm is_regular ?
	    $$mountoptions{loop}=1
	} elsif ($s->is_blockdevice) {
	    delete $$mountoptions{loop} # =0 is not enough if I'm taking keys..
	} else {
	    die "hm?, file '$device_path' is neither block device nor regular file"
	}
    }

    my $maybe_uuid= maybe_uuid_from_dev $device_path;
    my $id= defined($maybe_uuid)? $maybe_uuid : basename $device_path;

    ##(BUG: should be recursive instead of only one deep)
    if (my $luks= maybe_luks $device_path) {
	print $luks;
	my $devicemapper_path= "/dev/mapper/$id"; #ok diese id hier zu verwenden? well schon oder? oder sollte ich device name nehmen ?. well eben selber wählen. manuell auch ein name.  - aber ja wenn manuell verwend ich den namen beim mount point wieder.
	my $open= sub {
	    if (-f $device_path) {
		my $in=  Chj::IO::Command->new_sender("losetup","-fs",$device_path);
		my $new= $in->xcontent; chomp $new; #chop?
		$in->xxfinish;
		print "using loop device: '$new'\n";# to stderr? xprint ?
		#ah another (earlier) mutation:
		$device_path=$new;
	    }
	    xxsystem "cryptsetup", "luksOpen", $device_path, $id;
	    ##hm what with failed pw's? retry?.
	};
	if (-e $devicemapper_path) {
	    # check whether it's still working
	    my ($ok,$cnt)= try_file $devicemapper_path; # yeh it's a bit a misuse of that tool right?...
	    if ($ok) {
		#keep the file info, since "it is costly"(well it's
		#not but whatever; play this game to see what kind of
		#code results):
		$device_fileinfo= $cnt; # $device_path assignment follows for sure here. [currently..(becarefulaboutcoding (how do i tell this myslf uswusf))]
		#if ($cnt=~ )
		#no need to check.
		# already set up
	    } else {
		if ($cnt=~ m|Input/output error|i) {
		    xxsystem "cryptsetup", "luksClose", $devicemapper_path;
		    &$open
		} else {
		    die "unexpected error checking '$devicemapper_path': $cnt";#well doppelmoppel path in err msg.
		}
	    }
	} else {
	    &$open
	}
	# here comes the mutation: #careful, 'combine' this mentally with the setting of $device_fileinfo above.
	$device_path= $devicemapper_path;
	# hm and now get the id of the decrypted volume ?. or actually
	# hide that??. hide / ignore it for now and we get the "same
	# id for mapper and for mount point" behaviour.
	# Question: maybe I even *want* that behaviour of using the id
	# of the (outmost) *container*?: I could mount a copy twice
	# you "knw!"
    }

    if (not (defined $device_fileinfo)) {
	#(kind of catch-all-the cases above where I didn't ~thread through )
	$device_fileinfo= do {
	    # well almost copypaste from 'try_file $devicemapper_path' above but well.
	    my ($ok,$cnt)= try_file $device_path;
	    if ($ok) {
		$cnt
	    } else {
		die "error from try_file (cnt='$cnt')";#  better message?
	    }
	};
    }

    {
	# fsck: (as a side effect, thus in a sub scope.)
	if (my ($fstype)= $device_fileinfo=~ /\b(\w+) filesystem data/) {
	    if (lc ($fstype) eq "ext2") {
		if ($$mountoptions{ro}) {
		    print "Warning: not running fsck because of readonly option; be careful (don't 'remount,rw')\n";
		} else {
		    xxsystem "e2fsck", $device_path;
		}
	    } else {
		print "$myname thinks that no fsck is needed for filesystem type '$fstype'\n";# if verbose?
	    }
	} else {
	    #die "unknown filesystem type
	    die "couldn't find filesystem type in 'file' output '$device_fileinfo'";
	}
    }

    my $dest_path=  "/mnt/".$id;
    my $mount_on= sub ( $ ) {
	my ($dest_path)=@_;
	my $options= join(",", keys %$mountoptions);
	mount( (length $options ? ("-o",$options) : ()),
	       $device_path,
	       $dest_path);
    };
    my $private_dest_path= $dest_path."/mnt";
    if (-e $dest_path) {
	if (mounted $dest_path) {
	    mydie "there is already something mounted on '$dest_path'";
	} else {
	    if (-e $private_dest_path) {
		if (mounted $private_dest_path) {
		    mydie "there is already something mounted on '$private_dest_path'";
		} else {
		    &$mount_on( $private_dest_path);
		}
	    } else {
		if ($opt_private) {
		    mydie "--private option given but private '$private_dest_path' mount point does not exist (while '$dest_path' exists)"
		} else {
		    &$mount_on( $dest_path);
		}
	    }
	}
    } else {
	if ($opt_createdir) {
	    #or ask for it  ? wl or no, just rerun program then.with the option.
	    if ($opt_private) {
		xmkdir $dest_path, 0700;
		xmkdir $private_dest_path, 0700; #(perms for consistency only, not really relevant)
		&$mount_on( $private_dest_path);
	    } else {
		xmkdir $dest_path;
		&$mount_on( $dest_path);
	    }
	} else {
	    if ($ignore_if_mountpoint_missing) {
		if ($verbose) {
		    print "ignoring, mountpoint '$dest_path' does not exist.\n";
		    return; #u oh well. (to 'miss' the below print on purpose)
		}
	    } else {
		mydie "target dir '$dest_path' does not exist and --create-dir option not given"
	    }
	}
    }
    if ($verbose) {
	print "done.\n";
    }
}

use Chj::xperlfunc qw(xlstat);

{
    package CHJ::mount_easily::item;
    use Class::Array -fields=> -publica=>
      ('path','lstat');
    sub mtime {
	my $s=shift;
	$$s[Lstat]->mtime
    }
    end Class::Array;
}

if ($opt_auto) {
    if (@ARGV) {
	usage "both --auto and arguments given"
    } else {
	my $sortfn= sub {
	    my ($v)=@_;
	    $$v[1]->mtime
	      #only extractfn should it be named.
	};
	local our @items = sort {
	    #reverse sort
	    &$sortfn ($b) <=> &$sortfn ($a)
	} map {
	    my $s= xlstat $_;
	    bless [ $_, $s ], "CHJ::mount_easily::item"
	} glob "/dev/disk/by-uuid/*";
	local our @newitems;
	my $first= shift @items
	  or die "no items in /dev/disk/by-uuid/";
	push @newitems, $first;
	my $mtime= $first->mtime;
	for my $item (@items) {
	    if (($mtime - $item->mtime) > $auto_max_delay) {
		last;
	    } else {
		push @newitems, $item;
		$mtime= $item->mtime;
	    }
	}
	#use Chj::Backtrace; use Chj::repl; repl;
	mount_easily( $_->path, 1, 1) for @newitems;
    }
} else {
    if (@ARGV) {
	mount_easily( $_, (@ARGV > 1 or $verbose)) for @ARGV;
    } else {
	usage "neither --auto nor arguments given"
    }
}

#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;
