#!/usr/bin/perl -w

# Wed Sep 24 19:05:37 CEST 2008
(my $email='XXX%YYY,ch')=~ tr/%,/@./;

use strict;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname [-n outbasename] scanfiles

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
our $outbasename= "pdf";
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   "n=s"=> sub {
	       my ($n)=@_;
	       $n=~ m|/| and usage "no slashes allowed in outbasename";
	       $outbasename=$n
	   },
	   ) or exit 1;
usage unless @ARGV;

use Chj::xtmpdir;
use Chj::xperlfunc ':all'; # :all for basename
use Chj::xrealpath;
use Chj::xtmpfile;
use Chj::IO::Command;

sub mk_scale_into {
    my ($maxw,$maxh)=@_;
    my $arearatio= $maxw/$maxh;
    sub {
	my($origw,$origh)=@_;
	my $ratio= $origw/$origh;
	if ($ratio > $arearatio) {
	    # wider than  wasmmr"poss"
	    ($maxw, $maxw / $ratio)
	} else {
	    ($maxh * $ratio, $maxh)
	}
    }
}
our $scale= mk_scale_into(20, 28.28);
#use Chj::Backtrace; use Chj::repl; repl;
#__END__

my $outdir= xtmpdir;

#xchdir $outdir;

#assume we are converting everything everything to png.
my $i=0;
for my $file (@ARGV) {
    my $_bn= basename $file;
    my ($bn, $suffix)= $_bn=~ /(.*)\.([^.]+)\z/s
      or die "missing suffix in file '$file'";
    my $lcsuffix= lc $suffix;
    my $outpath= "$outdir/img_$i.png";
    if ($lcsuffix eq 'png') {
	my $abspath= xrealpath $file;
	xsymlink $abspath, $outpath;
    } else {
	xxsystem "convert", $file, $outpath;
    }
    $i++
}

{
    my $outfile= "$outdir/$outbasename.tex";
    my $out= xtmpfile $outfile;
    $out->xprint (<<'END');
\documentclass[a4paper]{article}
\usepackage{graphicx}
\usepackage[cm]{fullpage}\pagestyle{empty}
\usepackage[margin=0.1cm,noheadfoot]{geometry}
\begin{document}
END
    for my $i (0..($i-1)) {
	$out->xprint (<<'END') if $i;
\newpage
END
	my $name= "img_$i.png";
	my $path= "$outdir/$name";
	# keep ratio:
	my ($origw,$origh)= do {
	    my $in= Chj::IO::Command->new_sender("file","-L","--",$path);
	    my $cnt= $in->xcontent;
	    $in->xxfinish;
	    # tmp variable necessary -- it seems the "or" otherwise forces scalar mode.
	    # Perl is   fun
	    #or, an  [academic?na]experiment.F?
	    my @r= $cnt=~ m/PNG image, (\d+) x (\d+),/
	      or die "no match in '$cnt'";
	    @r
	};
	my ($w,$h)= &$scale($origw,$origh);
	$out->xprint (<<"END");
\\vspace*{0.01cm}
\\includegraphics[width=${w}cm,height=${h}cm]{$name}
END
    }
    $out->xprint (<<'END');
\end{document}
END
    $out->xclose;
    $out->xputback (0666 & ~umask);

    xchdir "$outdir";
    xxsystem ("pdflatex", "$outbasename.tex");
}




#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;
